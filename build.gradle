plugins {
    id "java-library"
    id "pmd"
    id 'maven-publish'
    id "com.github.spotbugs" version "1.6.6"
    id "edu.wpi.first.GradleRIO" version "2019.0.0-alpha-3"
}

group = 'frc.team166'

repositories {
    mavenCentral()
    maven {
        url 'http://first.wpi.edu/FRC/roborio/maven/development/'
    }
}

dependencies {
    api wpilib()
    implementation group: 'edu.wpi.first.ntcore', name: 'ntcore-java', version: '2018.4.1-20181003000242-1213-gfd82153'
    api group: 'edu.wpi.first.wpilibj', name: 'wpilibj-java', version: '2018.4.1-20181003000242-1213-gfd82153'
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
}

// Force Java 8 Compatibility mode for deployed code,
// in case the develoment system is using Java 10.
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

ext {
    SpotBugsTask = com.github.spotbugs.SpotBugsTask
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile('config/xsl/fancy.xsl')
    }
}

spotbugs {
    ignoreFailures = true
    excludeFilter = file("$rootProject.projectDir/config/findbugs-ignore.xml")
}

pmd {
    toolVersion = "6.10.0"
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("$rootProject.projectDir/config/pmd-ruleset.xml")
}

wrapper {
    gradleVersion = '4.10'
}

publishing {
    publications {
        chopshoplib(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}
