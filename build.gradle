import java.text.SimpleDateFormat

plugins {
    id "java-library"
    id "findbugs"
    id "pmd"
    id 'maven-publish'
    id "edu.wpi.first.GradleRIO" version "2019.0.0-alpha-3"
}

group = 'frc.team166'
version = '2019.0.0-alpha3-SNAPSHOT'

repositories {
    mavenCentral()
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
    api wpilib()
    api ctre()
    api navx()
    api group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
}

// Force Java 8 Compatibility mode for deployed code,
// in case the develoment system is using Java 10.
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile('config/xsl/fancy.xsl')
    }
}

findbugs {
    ignoreFailures = true
    excludeFilter = file("$rootProject.projectDir/config/findbugs-ignore.xml")
}

pmd {
    toolVersion = "6.7.0"
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("$rootProject.projectDir/config/pmd-ruleset.xml")
}

wrapper {
    gradleVersion = '4.10'
}

publishing {
    publications {
        chopshoplib(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'chopshoplib'
            url = "file://${buildDir}/repo"
        }
    }
}
